.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

.param vccx = 12.0
.param rinx = 0.1k
.param cix = 135u
.param r1x = 90k
.param r2x = 10k
.param rcx = 2.3k
.param rex = 0.12k
.param cbx = 1840u
.param routx = 0.33k
.param cox = 1200u
.param rlx = 0.008k

Vcc vcc 0 vccx
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 rinx

* input coupling capacitor
Ci in2 base cix

* bias circuit
R1 vcc base r1x
R2 base 0 r2x

* gain stage
Q1 coll base emit BC547A
Rc vcc coll rcx
Re emit 0 rex

* bypass capacitor
Cb emit 0 cbx


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc routx

* output coupling capacitor
Co emit2 out cox

* load
RL out 0 rlx

.op
.end

.control
*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

* time analysis
*tran 1e-5 1e-2
*plot v(coll)
*hardcopy vo1.ps vdb(coll)


* frequency analysis
ac dec 10 1 100MEG
plot vdb(coll)
plot vp(coll)
hardcopy gainplotdb.ps vdb(coll)
echo gainplotdb_FIG
hardcopy gainplotphase.ps vp(coll)*180/pi
echo gainplotphase_FIG


plot vdb(out)
plot vp(out)
hardcopy vdbout.ps vdb(out) vdb(in2)
echo vdbout_FIG

*input impedance in kohm
let zin = -v(in2)[40]/vin#branch[40]
let rez = Re(zin)
let imz = Im(zin)
let abszin = abs(zin)

echo "zin_TAB"
echo "Zi = $&rez + ($&imz)j"
echo "Zi = $&abszin"
echo "zin_END"

*plot abs(v(in2)[40]/vin#branch[40]/(-1000))

*cost
let rinx = 0.1k
let cix = 135u
let r1x = 90k
let r2x = 10k
let rcx = 2.3k
let rex = 0.12k
let cbx = 1840u
let routx = 0.33k
let cox = 1200u
let rlx = 0.008k

let cost = '(r1x + r2x + rinx + rcx + rex + routx + rlx)*10^(-3) + (cix + cbx + cox)*10^6 + 0.2'

*merit 
meas ac maxgaindb MAX vdb(out) from=1 to=100MEG

let maxgain = 10^(maxgaindb/20)

let cutoff = maxgaindb - 3

meas ac f1 WHEN vdb(out)=cutoff
meas ac f2 WHEN vdb(out)=cutoff CROSS=LAST

let bandwidth = f2-f1

let merit = (bandwidth*maxgain)/(f1*cost)

print maxgain
print bandwidth
print cost
print merit

echo "finalvaluesng_TAB"
echo "Gain = $&maxgain V"
echo "Gain (dB) = $&maxgaindb dB"
echo "Lower cut-off frequency = $&f1 Hz"
echo "Upper cut-off frequency = $&f2 Hz"
echo "Bandwidth = $&bandwidth Hz"
echo "Cost = $&cost MU"
echo "Merit = $&merit"
echo "finalvaluesng_END"

*need a different setup to measure output impedance

.endc 